(ns kotobase.engine.frontier-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotobase.engine.frontier :as frontier]
            [kotobase.engine.identity :as identity]))

(defn- commit [epoch parent]
  {:format identity/logical-commit-format
   :database-id "tenant/db"
   :epoch epoch
   :parents (cond-> [] parent (conj parent))
   :tx-root (str "tx-" epoch)
   :logical-checkpoint-root (str "checkpoint-" epoch)
   :schema-root "schema-1"
   :model-contract-root "model-1"
   :admission-policy-root "policy-1"})

(defn- entry [epoch parent]
  {:root (str "logical-" epoch)
   :commit (commit epoch parent)
   :signature (str "signature-" epoch)})

(defn- verified? [{:keys [root commit signature]}]
  (and (= root (str "logical-" (:epoch commit)))
       (= signature (str "signature-" (:epoch commit)))))

(def last-seen
  {:format frontier/frontier-format
   :database-id "tenant/db"
   :logical-commit-root "logical-1"
   :epoch 1})

(defn- problem-of [thunk]
  (try
    (thunk)
    nil
    (catch #?(:clj clojure.lang.ExceptionInfo :cljs js/Error) error
      (:problem (ex-data error)))))

(deftest tofu-and-idempotent-observation
  (let [initialized
        (frontier/accept-head
         {:candidate-root "logical-2"
          :path [(entry 2 "logical-1")]
          :verify-entry verified?})]
    (is (= :initialized (:status initialized)))
    (is (= 2 (get-in initialized [:frontier :epoch]))))
  (is (= {:status :unchanged :distance 0 :frontier last-seen}
         (frontier/accept-head
          {:last-seen last-seen :candidate-root "logical-1"
           :path [] :verify-entry verified?}))))

(deftest verified-descendant-advances-frontier
  (let [result
        (frontier/accept-head
         {:last-seen last-seen
          :candidate-root "logical-3"
          :path [(entry 3 "logical-2") (entry 2 "logical-1")]
          :verify-entry verified?})]
    (is (= :advanced (:status result)))
    (is (= 2 (:distance result)))
    (is (= "logical-3" (get-in result [:frontier :logical-commit-root])))))

(deftest rollback-equivocation-and-forks-fail-closed
  (testing "an older but otherwise valid commit is still a rollback"
    (is (= :rollback
           (problem-of
            #(frontier/accept-head
              {:last-seen last-seen :candidate-root "logical-0"
               :path [(entry 0 nil)] :verify-entry verified?})))))
  (testing "another root at the same epoch is explicit equivocation"
    (is (= :equivocation
           (problem-of
            #(frontier/accept-head
              {:last-seen last-seen :candidate-root "logical-1-fork"
               :path [{:root "logical-1-fork" :commit (commit 1 "logical-0")
                       :signature "signature-1"}]
               :verify-entry (constantly true)})))))
  (testing "a higher epoch is insufficient without ancestry"
    (is (= :not-descendant
           (problem-of
            #(frontier/accept-head
              {:last-seen last-seen :candidate-root "logical-3"
               :path [(entry 3 "fork-2")]
               :verify-entry verified?}))))))

(deftest proof-verification-and-chain-shape-are-mandatory
  (is (= :unverified-commit
         (problem-of
          #(frontier/accept-head
            {:last-seen last-seen :candidate-root "logical-2"
             :path [(entry 2 "logical-1")]
             :verify-entry (constantly false)}))))
  (is (= :noncontiguous-epoch
         (problem-of
          #(frontier/accept-head
            {:last-seen last-seen :candidate-root "logical-3"
             :path [(entry 3 "logical-1")]
             :verify-entry verified?}))))
  (is (= :database-mismatch
         (problem-of
          #(frontier/accept-head
            {:last-seen last-seen :candidate-root "logical-2"
             :path [(update (entry 2 "logical-1") :commit
                            assoc :database-id "other/db")]
             :verify-entry (constantly true)})))))
