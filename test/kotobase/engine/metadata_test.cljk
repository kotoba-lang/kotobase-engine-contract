(ns kotobase.engine.metadata-test
  (:require [clojure.test :refer [deftest is testing]]
            [ipld.core :as ipld]
            [kotobase.engine.metadata :as metadata]))

(defn- byte-array-of [xs]
  #?(:clj (byte-array xs)
     :cljs (js/Uint8Array. (clj->js (vec xs)))))

(defn- reverse-bytes [xs]
  (byte-array-of (reverse (seq xs))))

(deftest sealed-linked-segments-round-trip
  (let [blocks (atom {})
        put! (fn [cid payload] (swap! blocks assoc cid payload))
        a (metadata/persist-segment! put! reverse-bytes nil
                                     {:epoch 1 :secret "Alice"})
        b (metadata/persist-segment! put! reverse-bytes a
                                     {:epoch 2 :secret "Bob"})
        public-node (ipld/decode (get @blocks b))]
    (is (= [{:previous nil :delta {:epoch 1 :secret "Alice"}}
            {:previous a :delta {:epoch 2 :secret "Bob"}}]
           (metadata/restore-chain #(get @blocks %) reverse-bytes b)))
    (testing "the public envelope carries no plaintext metadata"
      (is (nil? (get public-node "history-edn")))
      (is (not-any? #{"Alice" "Bob"} (vals public-node))))))
